plugins {
    id 'java-library'
    id 'maven'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

repositories {
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.aikar.co/nexus/content/groups/aikar/" }
}

dependencies {
    api 'com.google.guava:guava:21.0'
    api 'com.google.inject:guice:4.2.2'

    api 'com.google.inject.extensions:guice-assistedinject:4.2.2'

    implementation 'io.github.classgraph:classgraph:4.8.47'

    if (project.hasProperty("ci")) {
        api files("lib/locales.jar")
        api files("lib/expiringmap-0.5.8.jar")
        api files("lib/acf-paper-0.5.0-SNAPSHOT.jar")
        api files("lib/acf-bukkit-0.5.0-SNAPSHOT.jar")
        api files("lib/acf-core-0.5.0-SNAPSHOT.jar")
    } else api "co.aikar:acf-paper:0.5.0-SNAPSHOT"

    api 'org.slf4j:slf4j-api:1.7.28'

    implementation 'org.slf4j:slf4j-simple:1.7.28'
    implementation 'com.github.knightzmc:mockbukkit:bcdf0db3df878c7b9f12cf4803d6a0190b92f200'


    api 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    implementation 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'


    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

}
compileKotlin {
    kotlinOptions {
        javaParameters = true
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

shadowJar {
    minimize()
    relocate 'co.aikar', 'me.bristermitten.aikar'
    exclude 'LICENSE-junit.txt'
    dependencies {
        exclude dependency('com.github.knightzmc:mockbukkit:.*')
        exclude dependency('org.spigotmc:spigot-api:.*')
        exclude dependency('org.spigotmc:spigot:.*')
        exclude dependency('org.bukkit:bukkit:.*')
        exclude dependency('net.md-5:bungeecord-chat:.*')
        exclude dependency('com.google.code.gson:gson:.*')
        exclude dependency('com.google.guava:guava:.*')
        exclude dependency('org.yaml:snakeyaml:.*')
        exclude dependency('commons-lang:commons-lang:.*')
    }
}

javadoc {
    source = sourceSets.main.allJava
    include '**/me/bristermitten/plumber/**'
    destinationDir = file('docs')
}
